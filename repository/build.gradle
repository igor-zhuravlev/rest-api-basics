plugins {
    id 'java-library'
    id "org.liquibase.gradle" version "2.0.4"
}

group 'com.epam.esm'
version '1.0'

def dbProperties = new Properties()
file('src/main/resources/database.properties').withInputStream {
    dbProperties.load(it)
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/liquibase/db.changelog-master.xml'
            driver dbProperties.get('jdbc.driver')
            url dbProperties.get('jdbc.url')
            defaultSchemaName dbProperties.get('jdbc.schema')
            username dbProperties.get('jdbc.username')
            password dbProperties.get('jdbc.password')
        }
    }
    runList = 'main'
}

dependencies {
    implementation project(':domain')

    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.18'

    liquibaseRuntime group: 'org.postgresql', name: 'postgresql', version: '42.2.18'
    liquibaseRuntime group: 'org.liquibase', name: 'liquibase-core', version: '4.2.2'

    implementation group: 'org.springframework', name: 'spring-context', version: "$springVersion"
    implementation group: 'org.springframework', name: 'spring-jdbc', version: "$springVersion"

    implementation group: 'org.springframework.data', name: 'spring-data-commons', version: '2.4.2'

    testImplementation group: 'org.springframework', name: 'spring-test', version: "${springVersion}"
    testImplementation group: 'com.zaxxer', name: 'HikariCP', version: '3.4.5'
    testImplementation group: 'com.h2database', name: 'h2', version: '1.4.200'
}
